# Maintainer: Dheeraj Vittal Shenoy <dheerajshenoy22@gmail.com>
pkgname=lektra-git
pkgver=0.6.0
pkgrel=1
pkgdesc="A fast, keyboard-based, configurable PDF reader"
arch=('x86_64')
url="https://codeberg.org/lektra/lektra"
license=('AGPL-3.0')
depends=('qt6-base' 'curl' 'libsynctex')
makedepends=('git' 'cmake' 'ninja' 'pkgconf') # 'bzr', 'git', 'mercurial' or 'subversion'
provides=("lektra")
conflicts=("lektra")
_mupdf_ver=1.27.0
source=(
    "lektra::git+https://codeberg.org/lektra/lektra.git"
    "mupdf-${_mupdf_ver}-source.tar.gz::https://mupdf.com/downloads/archive/mupdf-${_mupdf_ver}-source.tar.gz"
)
sha256sums=("SKIP" "ae2442416de499182d37a526c6fa2bacc7a3bed5a888d113ca04844484dfe7c6")

pkgver() {
    cd "$srcdir/lektra"
    git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "$srcdir/lektra"
    rm -rf external
    tar -xf "$srcdir/mupdf-${_mupdf_ver}-source.tar.gz"
    mkdir external
    mv "mupdf-${_mupdf_ver}-source" external/mupdf
}

build() {
    cd "$srcdir/lektra"

    # Build MuPDF
    make -C external/mupdf build=release HAVE_X11=no HAVE_GLUT=no

    cmake -S . -B build \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DENABLE_LLM_SUPPORT=OFF
    cmake --build build
}

package() {
    cd "$srcdir/lektra"
	DESTDIR="$pkgdir/" cmake --install build
}
